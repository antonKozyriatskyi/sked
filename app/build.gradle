apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    signingConfigs {
        r_config {
            def localPropsFile = rootProject.file("local.properties")

            if (localPropsFile.exists()) {
                logger.quiet("Found local.properties")

                def localProps = new Properties()
                localProps.load(localPropsFile.newDataInputStream())

                storeFile file(localProps.RELEASE_STORE_FILE)
                storePassword localProps.RELEASE_STORE_PASSWORD
                keyAlias localProps.RELEASE_KEY_ALIAS
                keyPassword localProps.RELEASE_KEY_PASSWORD
            } else {
                logger.quiet("local.properties not found. Trying to read from the env variables.")
                logger.quiet("Has keystore ${rootProject.file("keystore").exists()}")
                if (rootProject.file("keystore").exists()) {
                    rootProject.file("keystore").list().each {
                        logger.quiet("Found file: $it")
                    }
                }
                logger.quiet("Keystore path: ${System.getenv('KEYSTORE_FILE')}")

                storeFile file(System.getenv('KEYSTORE_FILE'))
                storePassword System.getenv('KEYSTORE_PASSWORD')
                keyAlias System.getenv('RELEASE_KEY_ALIAS')
                keyPassword System.getenv('RELEASE_KEY_PASSWORD')
            }
        }
    }
    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    defaultConfig {
        applicationId 'kozyriatskyi.anton.sked'
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 20
        versionName "1.2.2"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/db_schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.r_config
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
        }
        dev {
            initWith(debug)
            minifyEnabled true
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    dexOptions {
        javaMaxHeapSize "4G"
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    constraint_layout = '2.0.0-beta4'
    moxy = '2.0.2'
    dagger = '2.25.4'
    room = '2.2.3'
    rxjava2 = '2.1.2'
    rxandroid = '2.0.1'
    firebase = '17.2.1'
    rxrelay = '2.1.0'
    firebase_job_dispatcher = '0.8.5'

    junitVersion = '4.12'
    mockitoVersion = '1.10.19'
}

dependencies {
    implementation project(':sut-parser')

    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout"

    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-rxjava2:$room"
    kapt "androidx.room:room-compiler:$room"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "com.github.moxy-community:moxy:$moxy"
    implementation "com.github.moxy-community:moxy-androidx:$moxy"
    kapt "com.github.moxy-community:moxy-compiler:$moxy"

    implementation "com.google.dagger:dagger:$dagger"
    kapt "com.google.dagger:dagger-compiler:$dagger"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    implementation "io.reactivex.rxjava2:rxjava:$rxjava2"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid"
    implementation "com.jakewharton.rxrelay2:rxrelay:$rxrelay"

    implementation "com.firebase:firebase-jobdispatcher:$firebase_job_dispatcher"
    implementation "com.google.firebase:firebase-analytics:$firebase"

    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1') {
        transitive = true
    }

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-all:$mockitoVersion"
}

apply plugin: 'com.google.gms.google-services'