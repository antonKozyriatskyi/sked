apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    signingConfigs {
        r_config {
            def keystorePropsFile = rootProject.file("secrets/keystore.properties")

            if (keystorePropsFile.exists()) {
                logger.quiet("Reading keystore properties from keystore.properties")

                def props = new Properties()
                props.load(keystorePropsFile.newDataInputStream())

                storeFile file(props.RELEASE_STORE_FILE)
                storePassword props.RELEASE_STORE_PASSWORD
                keyAlias props.RELEASE_KEY_ALIAS
                keyPassword props.RELEASE_KEY_PASSWORD
            } else {
                logger.quiet("Reading keystore properties from env variables")

                storeFile file(System.getenv('KEYSTORE_FILE'))
                storePassword System.getenv('KEYSTORE_PASSWORD')
                keyAlias System.getenv('RELEASE_KEY_ALIAS')
                keyPassword System.getenv('RELEASE_KEY_PASSWORD')
            }
        }
    }
    compileSdkVersion 31
    buildToolsVersion '30.0.3'
    defaultConfig {
        applicationId 'kozyriatskyi.anton.sked'
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 20
        versionName "1.2.2"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/db_schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.r_config
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        coreLibraryDesugaringEnabled = true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    constraint_layout = '2.1.1'
    moxy = '2.2.2'
    dagger = '2.29.1'
    room = '2.3.0'
    rxjava2 = '2.2.10'
    rxandroid = '2.1.1'
    rxrelay = '2.1.0'
    preferences = '1.1.1'
    firebase_job_dispatcher = '0.8.5'
    coroutines = '1.5.2'

    junitVersion = '4.13'
    mockitoVersion = '1.10.19'
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"

    implementation project(':sut-parser')

    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.preference:preference:$preferences"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout"

    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-ktx:$room"
    implementation 'com.google.firebase:firebase-crashlytics:18.2.4'
    implementation 'com.google.firebase:firebase-analytics:20.0.0'
    kapt "androidx.room:room-compiler:$room"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "com.github.moxy-community:moxy:$moxy"
    implementation "com.github.moxy-community:moxy-androidx:$moxy"
    kapt "com.github.moxy-community:moxy-compiler:$moxy"

    implementation "com.google.dagger:dagger:$dagger"
    kapt "com.google.dagger:dagger-compiler:$dagger"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"

    implementation "com.firebase:firebase-jobdispatcher:$firebase_job_dispatcher"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-all:$mockitoVersion"
}