apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

repositories {
    maven { url 'https://jitpack.io' }
}

android {
    signingConfigs {
        release {
            def keystorePropsFile = rootProject.file("secrets/keystore.properties")

            if (keystorePropsFile.exists()) {
                logger.info("Reading keystore properties from keystore.properties")

                def props = new Properties()
                props.load(keystorePropsFile.newDataInputStream())

                storeFile rootProject.file(props.RELEASE_STORE_FILE)
                storePassword props.RELEASE_STORE_PASSWORD
                keyAlias props.RELEASE_KEY_ALIAS
                keyPassword props.RELEASE_KEY_PASSWORD
            } else {
                logger.info("Reading keystore properties from env variables")

                storeFile rootProject.file(System.getenv('KEYSTORE_FILE'))
                storePassword System.getenv('KEYSTORE_PASSWORD')
                keyAlias System.getenv('RELEASE_KEY_ALIAS')
                keyPassword System.getenv('RELEASE_KEY_PASSWORD')
            }
        }
    }
    compileSdkVersion 31
    buildToolsVersion '31.0.0'
    defaultConfig {
        applicationId 'kozyriatskyi.anton.sked'
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 31
        versionName "1.3.6"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/db_schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"

            splits.abi.enable = false
            splits.density.enable = false
            ext.alwaysUpdateBuildId = false
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileOptions {
        coreLibraryDesugaringEnabled = true
    }
}

ext {
    constraint_layout = '2.1.3'
    moxy = '2.2.2'
    dagger = '2.29.1'
    room = '2.3.0'
    rxjava2 = '2.2.10'
    rxandroid = '2.1.1'
    rxrelay = '2.1.0'
    preferences = '1.2.0'
    coroutines = '1.5.2'

    junitVersion = '4.13'
    mockitoVersion = '1.10.19'
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"

    debugImplementation 'com.github.haroldadmin:WhatTheStack:1.0.0-alpha02'

    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.preference:preference-ktx:$preferences"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout"
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-ktx:$room"
    kapt "androidx.room:room-compiler:$room"
    implementation 'com.google.firebase:firebase-crashlytics:18.2.8'
    implementation 'com.google.firebase:firebase-analytics:20.1.0'

    implementation "com.github.moxy-community:moxy:$moxy"
    implementation "com.github.moxy-community:moxy-androidx:$moxy"
    kapt "com.github.moxy-community:moxy-compiler:$moxy"

    implementation "com.google.dagger:dagger:$dagger"
    kapt "com.google.dagger:dagger-compiler:$dagger"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    implementation "com.squareup.moshi:moshi:1.13.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.13.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.3"

    implementation "androidx.work:work-runtime-ktx:2.7.1"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-all:$mockitoVersion"
}